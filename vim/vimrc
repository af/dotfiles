" General Notes
" * see ":h normal-index" or ":h insert-index" for a list of built-in mappings
" * see ":verbose nmap <C-j>" (for example) for maps setup by plugins or .vimrc

set nocompatible            " we're using Vim, not Vi

"===============================================================================
" NeoBundle setup
"===============================================================================
" Besides the bundles to install, assume that everything until the end of this
" section is required for NeoBundle's setup process.
if has('vim_starting')
   set runtimepath+=~/.vim/bundle/neobundle.vim/
endif

call neobundle#begin(expand('~/.vim/bundle/'))
NeoBundleFetch 'Shougo/neobundle.vim'

" vim plugins, managed by NeoBundle
NeoBundle 'kien/ctrlp.vim', 'b5d3fe66'
NeoBundle 'FelikZ/ctrlp-py-matcher', '799f37b'
NeoBundle 'mileszs/ack.vim', '6a57bc0'
NeoBundle 'tpope/vim-fugitive', '124550'
NeoBundle 'airblade/vim-gitgutter', '878c3422'
NeoBundle 'bling/vim-airline', 'aef500c426'
NeoBundle 'tomtom/tcomment_vim', '3d0a9975'
NeoBundle 'tpope/vim-surround', '42e9b46e'
NeoBundle 'jeetsukumaran/vim-filebeagle', 'abfb7f9d2'
NeoBundle 'tommcdo/vim-exchange', 'b82a774'
NeoBundle 'sheerun/vim-polyglot', '1c21231'     " syntax highlighting for many languages
NeoBundle 'vimwiki/vimwiki', '2c03d8'
NeoBundle 'justinmk/vim-sneak', '9eb89e43'
NeoBundle 'vim-scripts/YankRing.vim', 'a884f3a1'
NeoBundle 'tpope/vim-obsession', '4ab72e07ec'   " start a session file with :Obsession
NeoBundle 'ashisha/image.vim', 'ae15d1c5'       " view images in vim (requires `pip install Pillow`)

" Indentation, etc. Autodetect, but override with .editorconfig if present:
NeoBundle 'tpope/vim-sleuth', '039e2cd'
NeoBundle 'editorconfig/editorconfig-vim', '77875eff51'

" Javascript and HTML-related plugins
NeoBundle 'moll/vim-node', '07a5e9f91'      " Lazy loading doesn't work for some reason
NeoBundleLazy 'tristen/vim-sparkup', '1375ce1e7', {'autoload':{'filetypes':['html']}}
NeoBundleLazy 'tpope/vim-ragtag', {'autoload':{'filetypes':['html']}}   " Use <C-x>/ to close last open html tag

" Ultisnips (private snippets are stored in this repo)
NeoBundle 'UltiSnips', '3.0'

" theme/syntax related plugins:
NeoBundle 'scrooloose/syntastic', 'c1a209895'
NeoBundle 'colorizer', 'aae6b518'

" Colour schemes:
NeoBundle 'tomasr/molokai', 'e7bcec7573'        " default
NeoBundle 'junegunn/seoul256.vim', '340817d'    " softer, lower contrast
NeoBundle 'morhetz/gruvbox', 'ffe202e4'         " brown/retro. :set bg=dark

" plugins for colorscheme dev (not tested yet):
" https://github.com/shawncplus/Vim-toCterm
" https://github.com/guns/xterm-color-table.vim

call neobundle#end()
filetype plugin indent on
NeoBundleCheck
"===============================================================================
" (End of NeoBundle setup)
"===============================================================================



"===============================================================================
" General Vim settings
"===============================================================================
set visualbell
set history=500             " longer command history (default=20)
let mapleader = " "         " <leader> is our personal modifier key
set noswapfile              " Disable swap files
set backspace=indent,eol,start
"set directory=~/.vim/swp    " where the .swp files go (if enabled)

" File/buffer settings
autocmd BufWinEnter,BufNewFile * silent tabo    " Ensure only one tab is open
set hidden                  " TODO: revisit this. Hides instead of unloads buffers
set autoread                " reload files on changes (ie. changing git branches)
set encoding=utf-8
set scrolloff=3             " # of lines always shown above/below the cursor

" Indenting & white space
set expandtab
set tabstop=4 shiftwidth=4 softtabstop=4
set autoindent
set list listchars=tab:›\ ,trail:·          " mark trailing white space
"set list listchars=tab:›\ ,trail:·,eol:¬   " mark trailing white space (with eol)


"===============================================================================
" Display/window settings
"===============================================================================
syntax on
set bg=dark
set number                  " line number gutter
set ruler                   " line numbers at bottom of page
set showcmd
set title
set wildmenu
set wildignore=.svn,.git,.gitignore,*.pyc,*.so,*.swp,*.jpg,*.png,*.gif,node_modules,_site
set laststatus=2            " Always show a status line for lowest window in a split
set cursorline              " highlight the full line that the cursor is currently on
set colorcolumn=80,100      " Highlight these columns with a different bg
set helpheight=99999        " Hack to make help pages open "fullscreen"

" Automatically set quickfix height
" http://vim.wikia.com/wiki/Automatically_fitting_a_quickfix_window_height
au FileType qf call AdjustWindowHeight(3, 30)       " 2nd arg=> max height
function! AdjustWindowHeight(minheight, maxheight)
    exe max([min([line("$"), a:maxheight]), a:minheight]) . "wincmd _"
endfunction

"===============================================================================
" Searching & Replacing
"===============================================================================
set ignorecase
set smartcase               " override 'ignorecase' if search term has upper case chars
set incsearch               " incremental search
set showmatch
set hlsearch                " highlight searched items
set gdefault                " use 'global' mode by default for substitutions

" Clear search highlighting with ' ,', use python-style search regexes:
nnoremap <leader>, :noh<cr>
nnoremap / /\v
vnoremap / /\v
nnoremap ? ?\v
vnoremap ? ?\v


"===============================================================================
" Line wrapping
"===============================================================================
set wrap
set textwidth=99
set formatoptions=qrn1
nnoremap j gj
nnoremap k gk


"===============================================================================
" Colorscheme
"===============================================================================
color molokai

" Molokai overrides for commit preview highlighting (eg. after "git commit"):
hi link diffAdded Function
hi diffRemoved guifg=#960050 ctermfg=162
hi link diffLine Define
hi link diffFile Define
hi link diffSubname Constant

" MacVim/GVIM and 256-colour term overrides
if has('gui_running')
    set guifont=Monaco:h14      " gvim/mvim: Bump up the default fontsize
elseif $TERM == "xterm-256color" || $TERM == "screen-256color" || $COLORTERM == "gnome-terminal"
    set t_Co=256            " Richer colours if our terminal can handle it.
endif

" Show syntax highlighting groups for word under cursor with <leader>s
" From Vimcasts #25: http://vimcasts.org/episodes/creating-colorschemes-for-vim/
nnoremap <leader>h :call <SID>SynStack()<CR>
function! <SID>SynStack()
  if !exists("*synstack")
    return
  endif
  echo map(synstack(line('.'), col('.')), 'synIDattr(v:val, "name")')
endfunc


"===============================================================================
" Plugin customization
"===============================================================================

" FileBeagle
let g:filebeagle_show_hidden = 1        " Use 'gh' to toggle- FileBeagle hides lots by default

" Load matchit.vim, but only if the user hasn't installed a newer version.
if !exists('g:loaded_matchit') && findfile('plugin/matchit.vim', &rtp) ==# ''
    runtime! macros/matchit.vim
endif

" Ctrl-P
nnoremap <C-t> :CtrlPBuffer<CR>             " Search active buffers
nnoremap <C-m> :CtrlPMRUFiles<cr>
let g:ctrlp_map = '<leader><leader>'    " Search in current directory
let g:ctrlp_open_new_file = 'r'
let g:ctrlp_open_multiple_files = 'i'
let g:ctrlp_clear_cache_on_exit = 0
let g:ctrlp_user_command = 'ag %s -i --nocolor --nogroup --hidden --ignore .git --ignore node_modules -g ""'
" PyMatcher for CtrlP:
if !has('python')
    echo 'In order to use pymatcher plugin, you need +python compiled vim'
else
    let g:ctrlp_match_func = { 'match': 'pymatcher#PyMatch' }
endif

" gitgutter
" OS X's default vim doesn't have signs support, so disable it for that editor.
if has('signs')
    map! <C-j> <esc>:GitGutterNextHunk<CR>
    map <C-j> :GitGutterNextHunk<CR>
    map! <C-k> <esc>:GitGutterPrevHunk<CR>
    map <C-k> :GitGutterPrevHunk<CR>
else
    let g:gitgutter_enabled = 0
endif

" Syntastic display customizations:
" TODO: update colors so they work in console vim also
let syntastic_mode_map = { 'passive_filetypes': ['html'] }  " disable html checking by default
let g:syntastic_always_populate_loc_list = 1
let g:syntastic_auto_loc_list = 1
let g:syntastic_check_on_open = 1
let g:syntastic_check_on_sq = 0
let g:syntastic_error_symbol = '!!'
let g:syntastic_warning_symbol = '!!'
hi SyntasticErrorSign guifg=#ff0000
hi SyntasticWarningSign guifg=#ff0000
map <C-e> :lnext<CR>

" Syntastic checker config:
let g:syntastic_python_checkers=['pyflakes']

" vim-airline:
" Note: the following symbols require a patched font.
" For Monaco, I used https://github.com/fromonesrc/monaco-powerline-vim
let g:airline_left_sep          = '⮀'
let g:airline_left_alt_sep      = '⮁'
let g:airline_right_sep         = '⮂'
let g:airline_right_alt_sep     = '⮃'
let g:airline_branch_prefix     = '⭠'
let g:airline_readonly_symbol   = '⭤'
let g:airline_linecolumn_prefix = '⭡'
let g:airline_theme = 'murmur'
let g:airline#extensions#tabline#enabled = 1    " Tab line at top of window

" Colorizer
nnoremap <leader><F2> :ColorToggle<CR>

" Sparkup
let g:sparkupExecuteMapping = '<C-e>'       " The default mapping

" UltiSnips
let g:UltiSnipsEditSplit = 'vertical'
let g:UltiSnipsSnippetDirectories = ['personal_snippets']
let g:UltiSnipsSnippetsDir = '~/.vim/personal_snippets'
nnoremap <leader>s :UltiSnipsEdit<CR>

" vim-stringify
map <leader>' :call Stringify()<CR>

" Ack.vim
nnoremap <leader>g :Ack 
set shellpipe=>             " Prevents results from flashing during search
let g:ackhighlight = 1      " Highlight results
if executable('ag')
    let g:ackprg = 'ag'
    let g:ack_default_options = '--nocolor --nogroup --hidden --ignore .git --ignore node_modules -g ""'
endif

" VimWiki
let g:vimwiki_ext2syntax = {}
let g:vimwiki_list = [{'path': '~/vimwiki/', 'syntax': 'markdown', 'ext': '.md',
                    \  'diary_rel_path': 'journal/', 'diary_index': 'index',
                    \  'diary_header': 'Journal', 'diary_sort': 'asc'},
                    \ {'path': '~/code/reelhouse/wiki/', 'syntax': 'markdown', 'ext': '.md'}]

"===============================================================================
" Key Bindings: Moving around
"===============================================================================

" (built-in)
" <C-o> - move the cursor position back in the jump list
" <C-i> - move the cursor position forward in the jump list
" g;    - move back in the change list
" g,    - move forward in the change list
" gi    - move to the last insert, and re-enter insert mode
" {     - move back one paragraph
" }     - move forward one paragraph

" Navigating between buffers:
nmap <C-h> :bp<CR>
nmap <C-l> :bn<CR>
nnoremap <silent> <C-u> :bd<CR>
nmap <C-q> :1,100bd<CR>

" Save current file every time we leave insert mode or hit <esc>:
" Note that the autocmd repeats the mapping each time we leave insert mode,
" this doesn't seem to be a problem in practice.
"
" Alternative approaches (which didn't seem to work as well for my needs):
" set autowriteall   (couldn't get this to work)
" :au FocusLost * :wa
" autocmd InsertLeave * if expand('%') != '' | update | endif
" (last one via http://blog.unixphilosopher.com/2015/02/five-weird-vim-tricks.html)
inoremap <esc> <esc>:w<CR>
autocmd InsertLeave * nnoremap <esc> <esc>:w<CR>

" Swap ` and ' for mark jumping:
nnoremap ' `
nnoremap ` '

" Global mark conventions
" Uppercase marks persist between sessions, so they're useful for accessing
" common files quickly. By convention, use the following global marks:
"
" V     - vimrc
" Z     - zshrc
" J     - jshintrc
"
" ProTip: After opening a file with a global mark, you can change vim's cwd to
" the file's location with ":cd %:h"


"===============================================================================
" Key Bindings: Misc
"===============================================================================
" Use ':w!!' to save a root-owned file using sudo:
cmap w!! w !sudo tee % >/dev/null

set foldlevelstart=10
set pastetoggle=<C-y>   " Had problems with <F2>, see http://stackoverflow.com/q/7885198/351433

" copy/paste with system clipboard:
vmap <Leader>y "+y
vmap <Leader>d "+d
nmap <Leader>p "+p
nmap <Leader>P "+P
vmap <Leader>p "+p
vmap <Leader>P "+P


"===============================================================================
" Filetype-specific settings
"===============================================================================
" filetype detection for syntax highlighting
au BufNewFile,BufRead *.md set filetype=markdown
au BufNewFile,BufRead *.mustache set filetype=mustache
autocmd FileType mustache set ft=html.mustache

" JSON files: set filetype to json for syntastic, use js highlighting:
au BufRead,BufNewFile *.json set filetype=json
au BufRead,BufNewFile *.json setlocal syntax=javascript


"===============================================================================
" Misc
"===============================================================================

" Vim supports encryption! Just enter :X before saving.
" But we need to update the encryption type to a more secure one.
" See http://usevim.com/2013/11/01/vim-encryption/
" Note: disabled because Neovim does not support cryptmethod (https://github.com/neovim/neovim/issues/701)
"set cryptmethod=blowfish

" Set vim's cwd to the closest ancestor dir containing a .git directory
" using "git rev-parse --show-toplevel"
function! MoveToGitDir()
  let filePath = fnamemodify(bufname("%"), ':p:h')
  exe 'cd' fnameescape(filePath)
  let repoPath = system("git rev-parse --show-toplevel")
  let repoPath = substitute(repoPath, '\n$', '', '')    " Remove newline from system() output
  exe 'cd' fnameescape(repoPath)
  echo 'Changed dir to ' . repoPath
endfunc
nnoremap <leader>d :call MoveToGitDir()<CR>
